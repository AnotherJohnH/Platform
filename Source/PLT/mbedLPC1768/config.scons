#-------------------------------------------------------------------------------
#  Copyright (c) 2017 John D. Haughton
# 
#  Permission is hereby granted, free of charge, to any person obtaining a copy
#  of this software and associated documentation files (the "Software"), to deal
#  in the Software without restriction, including without limitation the rights
#  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
#  copies of the Software, and to permit persons to whom the Software is
#  furnished to do so, subject to the following conditions:
# 
#  The above copyright notice and this permission notice shall be included in
#  all copies or substantial portions of the Software.
# 
#  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
#  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
#  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
#  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
#  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
#  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
#  SOFTWARE.
#-------------------------------------------------------------------------------

# scons configuration for MBED LPC1768 builds

PREFIX  = 'arm-none-eabi-'
MC='armv7m'
CHIP='LPC1768'

#-------------------------------------------------------------------------------

Import('env')

env['machine']    = MC
env['OBJPREFIX']  = 'obj/'+env['machine']+'/'
env['LIBPREFIX']  = 'lib/'+env['machine']+'/lib'
env['PROGPREFIX'] = 'bin/'+env['machine']+'/'

env['AS']        = PREFIX+'as'
env['CC']        = PREFIX+'gcc'
env['CXX']       = PREFIX+'g++'
env['LINK']      = PREFIX+'ld'
env['AR']        = PREFIX+'ar'
env['RANLIB']    = PREFIX+'ranlib'
env['OBJCOPY']   = PREFIX+'objcopy'
env['OBJDUMP']   = PREFIX+'objdump'
env['SIZE']      = PREFIX+'size'

env['ASFLAGS']   = ['-mcpu=cortex-m3']

env['CCFLAGS']   = ['-DNDEBUG',
                    '-DNCONSOLE',
                    '-mcpu=cortex-m3',
                    '-mthumb',
                    '-mfloat-abi=soft',
                    '-fno-common',
                    '-fno-builtin',
                    '-fmessage-length=0',
                    '-fno-default-inline',
                    '-fno-exceptions',
                    '-ffunction-sections',
                    '-fdata-sections']

env['CXXFLAGS']  = ['-std=c++11', '-fno-rtti', '-DNO_RTTI']

import os
p = os.popen(env['CC'] + ' -print-file-name=armv7-m')
gcc_lib = p.readline()
env['LIBPATH']   = Split(gcc_lib)

env.Append(LIBS = ['tinyc', 'gcc'])
env['LINKFLAGS'] = ['--static']

env['startup'] = env.Object('../../MTL/chip/'+CHIP+'/startup.s')
env['script']  = env.File('../../MTL/chip/'+CHIP+'/script.ld')

Import('source')

source+=[env.File('platform.cpp'),
         env.File('../Stub/Audio.cpp'),
         env.File('../Stub/Event.cpp'),
         env.File('Frame.cpp'),
         env.File('Image.cpp'),
         env.File('../Stub/Midi.cpp'),
         env.File('../Stub/Sounder.cpp'),
         env.File('../Stub/File.cpp'),
         env.File('../../MTL/chip/LPC1768/PALVideo.cpp')]

#-------------------------------------------------------------------------------
# Adapt the Program() builder to generate a raw binary and some debug info

env['BUILDERS']['Program'].action += '$OBJCOPY -O binary $TARGET ${TARGET}.bin; '+ \
                                     '$OBJDUMP -D $SOURCE > ${TARGET}.dbg; '+ \
                                     '$SIZE $TARGET'

#-------------------------------------------------------------------------------
# Declare platform specific applications

env['app_src'] = ['MTL/chip/LPC1768/testI2S.cpp',
                  'MTL/chip/LPC1768/testPWM.cpp',
                  'MTL/chip/LPC1768/testPixelGen.cpp',
                  'MTL/chip/LPC1768/testPALVideo.cpp',
                  'PLT/test/testAudioOut.cpp',
                  'PLT/test/testAudioIn.cpp',
                  'PLT/test/testMidiIn.cpp',
                  'PLT/test/testCanvas.cpp',
                  'PLT/test/testEvent.cpp',
                  'PLT/test/testFrame.cpp',
                  'GUI/testGui.cpp',
                  'GUI/testGuiFrame.cpp',
                  'GUI/testGuiHelloWorld.cpp',
                  'TRM/testTerminalCanvas.cpp']
